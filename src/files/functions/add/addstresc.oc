

#errnr
Function addtosecstresc(sd pcontent,sd psize,sd sz,sd escapes,sd pdest)
	callret addtosecstresc_base(pcontent,psize,sz,escapes,pdest,(NULL))
endFunction
#errnr
Function addtosecstresc_xfile_ifif(sd pcontent,sd psize,sd sz,sd escapes,sd pdest)
	sd xfile1;sd xfile2;sd xfile3
	sd err;set err addtosecstresc_base(pcontent,psize,sz,escapes,pdest,#xfile1)
	if err=(noerror)
		callret xfile_add_multistring_ifif(xfile2,xfile1,xfile3)
	endif
	return err
endFunction
#err
Function addtosecstresc_xfile_if(sd pcontent,sd psize,sd sz,sd escapes,sd pdest)
	sd xfile1;sd xfile2;sd xfile3
	sd err;set err addtosecstresc_base(pcontent,psize,sz,escapes,pdest,#xfile1)
	if err=(noerror)
		callret xfile_add_multistring_if(xfile2,xfile1,xfile3)
	endif
	return err
endFunction
#errnr
Function addtosecstresc_base(sd pcontent,sd psize,sd sz,sd escapes,sd pdest,sv p_xfile)
	#set destination start
	Data destReg#1
	Data ptrdestReg^destReg
	Call getcontReg(pdest,ptrdestReg)

	Data odd#1
	Data zero=0

	# size of the string out with term
	Data sizeEsc#1
	Set sizeEsc sz
	sd end;set end sizeEsc
	Sub sizeEsc escapes
	if p_xfile!=(NULL)
		set p_xfile# sizeEsc
		incst p_xfile
	endif
	Inc sizeEsc

	Set odd zero
	#into idata string is padded to word
	If p_xfile=(NULL) #wordpad=0 is a coincidence that is same as no p_xfile
		set odd sizeEsc
		and odd 1
		If odd!=zero
			Inc sizeEsc
		EndIf
	EndIf

	Data noerr=noerror
	Data errnr#1
	SetCall errnr addtosec(0,sizeEsc,pdest)
	If errnr!=noerr
		Return errnr
	EndIf

	#set destination start
	Str destloc#1
	Data ptrdestloc^destloc
	Call getcont(pdest,ptrdestloc)
	Add destloc destReg
	if p_xfile!=(NULL)
		set p_xfile# destloc
		incst p_xfile
		set p_xfile# (Xfile_string_multiline_no)
	endif

	add end pcontent#
	While pcontent#!=end
		Char byte#1
		SetCall byte quotescaped(pcontent,psize,p_xfile)
		Set destloc# byte
		Inc destloc
		Call stepcursors(pcontent,psize)
	EndWhile
	Set destloc# zero
	If odd!=zero
		Inc destloc
		Set destloc# zero
	EndIf

	call stepcursors(pcontent,psize)

	Return (noerror)
EndFunction
