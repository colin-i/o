
#err
function endfunction(sd parses)
	sd err
	if parses=(pass_init)
		if main.innerfunction=(FALSE)
			Char unexef="Unexpected ENDFUNCTION command."
			Str unexeferr^unexef
			return unexeferr
		endif
		setcall err xfile_proto_done()
		if err!=(noerror)
			return err
		end
		Set main.innerfunction (FALSE)
		call vars_log_reset() #to let next variables be "o.var"
	elseif parses=(pass_write)
		setcall err xfile_add_end_if()
		if err=(noerror)
			setCall err checkcondloopclose()
			If err=(noerror)
				if main.ret_cont_break=(ret_cont_break_no)
					call xfile_fndef_structend_if((el_or_e_nothing))

					data ptrreturn#1
					data sizereturn#1
					data ptrptrreturn^ptrreturn
					setcall sizereturn getreturn(ptrptrreturn)
					SetCall err addtoCode_set_programentrypoint(ptrreturn,sizereturn)
					If err!=(noerror);return err;endif
				elseif main.ret_cont_break=(ret_cont_break_mustskip)
					return "Previous RET was useless."
				else
					call set_programentrypoint()
					if main.ret_cont_break=(ret_cont_break_canskip_warn)
						whiletrue
							if main.xbool=(Yes_strict)
								if main.callret_end_warn=(No)
									break
								end
							end
							call Message("CallRet;EndFunction warning")
							break
						end
					end
					#else ret_cont_break_canskip
				endelse
				setcall err scopes_store(main.functionTagIndex)
				If err=(noerror)
					sd i
					Set i 0
					While i!=(numberofvars)
					#here is clearing indexes to not call variables from this scope in the parent scope
						Data containertoclear#1
						SetCall containertoclear getstructcont(i)
						Data indexptr#1
						Data ptrindexptr^indexptr
						Call getptrcontReg(containertoclear,ptrindexptr)
						Set indexptr# 0
						Inc i
					EndWhile
				else
					return err
				endelse
			else
				return err
			endelse
			Set main.innerfunction (FALSE)
		else
			return err
		endelse
	endelseif
	inc main.functionTagIndex
	return (noerror)
endfunction
