
importx "fseek" fseek
importx "ftell" ftell
importx "rewind" rewind
importx "malloc" malloc
importx "fread" fread
importx "free" free
importx "fwrite" fwrite

importx "printf" printf
importx "fprintf" fprintf
importx "memcmp" memcmp
importx "sprintf" sprintf

include "xfile.oh"
const Xfile_aftercall_declarative=Xfile_aftercall_import # less or equal
const Xfile_cond_first=Xfile_cond_greatequalunsign
include "ascii.oh"

include "mem.oc"

const err=-1
const ok=0
const long32=DWORD

const allowwrite_yes=0
const allowwrite_no=1
const allowwrite_no_include=2
const ignored_no=0
const ignored_yes=1
const ignored_depending_on_tags=2

datax result#1

function loop(sd pointer,sd delim,sd output,sd ignores)
	valuex bp#1
	base bp

	charx has_entry#1
	set has_entry (FALSE)
	charx allowwrite#1
	set allowwrite (allowwrite_yes)
	charx directwrite#1
	set directwrite (TRUE)
	valuex function_start#1
	set function_start (err)
	valuex function_start_base#1
	set function_start_base (err)
	charx innerfunction#1
	set innerfunction (FALSE) #function_start is only until first return, and is not at structs
	charx indentation#1
	set indentation 0
	charx same_line#1
	set same_line (FALSE)
	charx hotgroup#1
	set hotgroup 0
	datax szfunctiondef#1
	set szfunctiondef 0
	datax icount#1  #dependent on includesReg
	set icount 0

	sd nextisignored=ignored_depending_on_tags

	#if delim>^(long32)
	vdatax entrystart#1
	vstrx entrychars#1
	set entrystart pointer#
	add entrystart pointer
	set entrychars entrystart
	add entrychars (DWORD)

	add pointer (long32)
	sub delim (long32)
	add delim pointer
	while pointer!=delim
		charx command#1;call get_char(#pointer,#command)
		if command=(Xfile_noignore)
			set nextisignored (ignored_no)
		elseif command=(Xfile_yesignore)
			set nextisignored (ignored_yes)
		elseif command=(Xfile_variadic)
			call variadic(#pointer,output)
		else
			whiletrue
				if command!=(Xfile_end)
					if command!=(Xfile_else)
						if command!=(Xfile_elseif)
							break
						end
					end
				end
				dec indentation
				break
			end
			if same_line=(FALSE)
				sd i;set i indentation
				while i>0
					char a=asciitab
					call write(output,#a,1)
					dec i
				end
			else
				set same_line (FALSE)
			end

			value Xfile_proto^proto
			value *Xfile_import^import
			value *Xfile_comment^comment
			value *Xfile_commentmulti^commentmulti
			value *Xfile_commentlineend^commentlineend
			value *Xfile_format^format
			value *Xfile_include^include
			value *Xfile_functiondef^functiondef   #in c, this can cause implicit declaration if a{b()} b{} but is only a warning, will be resolved ok
			value *Xfile_declare^declare
			value *Xfile_action^action
			value *Xfile_action2^action2
			value *Xfile_call^call
			value *Xfile_callex^callex
			value *Xfile_if^if
			value *Xfile_elseif^elseif
			value *Xfile_else^else
			value *Xfile_while^while
			value *Xfile_whiletrue^whiletrue
			value *Xfile_break^break
			value *Xfile_continue^continue
			value *Xfile_end^end
			value *Xfile_ret^ret
			value *Xfile_library^library
			value *Xfile_import_name^import_name
			value *Xfile_aftercall^aftercall
			value *Xfile_hex^hex
			value *Xfile_override^override
			value *Xfile_orphan^orphan
			value *Xfile_interrupt^interrupt
			value *Xfile_line^line
			value *Xfile_iend^iend
			datau mults={1,2,4,8,0x10,0x20,0x40,0x80,0x100,0x200,0x400,0x800,0x1000,0x2000,0x4000,0x8000,0x10000,0x20000,0x40000,0x80000}
			datau     *={0x100000,0x200000,0x400000,0x800000,0x1000000,0x2000000,0x4000000,0x8000000,0x10000000,0x20000000,0x40000000} #,0x80000000}

			datax ignored#1
			if nextisignored=(ignored_depending_on_tags)
				sd igpow=DWORD;mult igpow command
				sd x^mults;add x igpow
				set ignored x#
				and ignored ignores
			else # yes/no
				set ignored nextisignored
				set nextisignored (ignored_depending_on_tags)
			end

			mult command :
			sv dest^Xfile_proto;add dest command
			set dest dest#
			call dest(output,#pointer)

			if hotgroup>0
				if dest!=line
					dec hotgroup
				end
			end
		end
	end
	if main.result=(EXIT_SUCCESS)
		if has_entry=(TRUE)
			callg writecrtn(output)
		end
	end
end

#exit
function preloop(sd input,sd output,sd ignores)
	set main.result (EXIT_FAILURE)
	call fseek(input,0,(SEEK_END))
	sd size;set size ftell(input) #will not decrease if claimed here(is still same place if file deleted in parallel)
	if size!=-1  #lseek and same result (remark fileno)
		call rewind(input)
		sd buffer;set buffer malloc(size)
		if buffer!=(NULL)
			call fread(buffer,size,1,input)
			#if r=1 why? already claimed by ftell
			sd r
			set r mems()
			if r=(TRUE)
				if size>^(long32)
					set main.result (EXIT_SUCCESS)
					call loop(buffer,size,output,ignores)
				end
				call memfree()
			end
			call free(buffer)
		end
	end
	return main.result
end

function get_char(sv pbuffer,ss pchar)
	ss buffer;set buffer pbuffer#
	set pchar# buffer#
	add pbuffer# (Xfile_sz_char)
end

function get_data(sv pbuffer,sd pdata)
	sd buffer;set buffer pbuffer#
	set pdata# buffer#
	add pbuffer# (Xfile_sz_int)
end

function get_string(sv pbuffer,sd psize,sv ppointer)
	call get_data(pbuffer,psize)
	set ppointer# pbuffer#
	add pbuffer# psize#
end
#S structure
function get_multistring(sv pbuffer,ss multiline,sd sz,sv pointer)
	call get_char(pbuffer,multiline)
	call get_string(pbuffer,sz,pointer)
end

function fseek_if(sd outfile,sd i)
	if loop.allowwrite=(allowwrite_yes) #can be no_include
		call fseek(outfile,i,(SEEK_CUR))
	end
end
function backchar(sd outfile)
	call fseek_if(outfile,-1)
end
function backindent(sd outfile)
	sd i=1
	mult i loop.indentation  ##0 or more
	neg i
	call fseek_if(outfile,i)
end
function line_continuation(sd outfile)
	call backindent(outfile)
	call backchar(outfile) #nl
end

function line(sd outfile) #this with the functions but is throwless
	call line_continuation(outfile)
	set loop.same_line (TRUE)
end

include "util.oc"

function aftercallenable()
	set main.result (EXIT_FAILURE)
	leave loop.bp
	#aftercallenable
end

function signed()
end


#aftercalli *

function unimplemented(ss reason)
	call printf("unimplemented %s\n",reason)
	sd b;set b write_allow();if b=(TRUE)
		call aftercallenable()
	end
end

function write_long_pointer(sd outfile,sd pointer,sd size)
	call write(outfile,main.long,main.long_cast_size)
	callg write(outfile,pointer,size)
end
function write_long_point(sd outfile)
	callg write(outfile,main.long,main.long_cast_size)
end
function write_long(sd outfile)
	callg write(outfile,main.long,main.long_size)
end

function comment(sd outfile,sv pbuffer)
	char a={asciislash,asciislash}
	call write(outfile,#a,2)
	datax sz#1;valuex pointer#1
	call get_string(pbuffer,#sz,#pointer)
	if sz!=0
		call write(outfile,pointer,sz)
	end
	callg writenl(outfile)
end
function functiondef(sd outfile,sv pbuffer)
	set loop.innerfunction (TRUE)

	value intern^functiondef_intern
	value *raw^functiondef_raw
	value *extern^functiondef_extern
	value *entry^functiondef_entry

	charx type#1;call get_char(pbuffer,#type)
	charx vargs#1;call get_char(pbuffer,#vargs)
	valuex pointer#1
	call get_string(pbuffer,#loop.szfunctiondef,#pointer)

	mult type :
	sv dest^intern;add dest type
	set dest dest#
	callg dest(pbuffer,outfile,pointer,loop.szfunctiondef,vargs)
end
function iend()
	if loop.icount!=0
		dec loop.icount
		if loop.icount=0
			and loop.allowwrite (~allowwrite_no_include)
		end
	end
end

include "action.oc"
include "decl.oc"
include "cond.oc"
include "number.oc"
include "var.oc"

function commentmulti(sd outfile,sv pbuffer)
	char b=asciiasterisk
	char a={asciislash,asciiasterisk}
	call write(outfile,#a,2)
	datax sz#1;valuex pointer#1
	call get_string(pbuffer,#sz,#pointer)
	if sz!=0
		call write(outfile,pointer,sz)
	end
	callg writen(outfile,#b,2)
end
function commentlineend(sd outfile,sv pbuffer)
	call line_continuation(outfile)
	callg comment(outfile,pbuffer)
end
function format(sd *outfile,sv pbuffer) #will do nothing at the moment
	charx fmt#1;call get_char(pbuffer,#fmt)
end
function include(sd outfile,sv pbuffer)
	charx type#1;call get_char(pbuffer,#type)

	char s="#include "^
	call write(outfile,#s,\.s)
	call write_string_ex(pbuffer,outfile) #new line is useless: there are no escape chars in c include and, also: any member of the source character set except new-line and (quote or >)
	call writenl(outfile)

	charx alternative#1;call get_char(pbuffer,#alternative)
	if alternative=(Xfile_include_alternative_yes)
		charx multi#1;datax sz#1;valuex pointer#1
		call get_multistring(pbuffer,#multi,#sz,#pointer)
	end

	inc loop.icount
	or loop.allowwrite (allowwrite_no_include)
end
function declare(sd outfile,sv pbuffer)
	charx type#1;charx field#1;charx constif#1
	call decl(pbuffer,outfile,#field,#type,#constif)
	datax sz#1;valuex pointer#1
	charx sign#1;call get_char(pbuffer,#sign)
	if sign=(Xfile_declsign_reserve)
		set loop.hotgroup 2
		#as a special case, [1] is removed for C. and whit what replaced? const a=1 and [a]. alternatives? structure with declared vars, = at nobits vars
		#this for structures, and not only because at o #1 is not [1], will make more similar
		or loop.allowwrite number_allowwrite(pbuffer#)
		call writesl(outfile)
		call number(pbuffer,outfile)
		call writesr(outfile)
		and loop.allowwrite (~allowwrite_no)  # example: at unimplemented commands and at condition sign check is FALSE
	elseif sign=(Xfile_declsign_pointer)
		call writee(outfile)
		charx atr#1;call get_char(pbuffer,#atr)
		if atr=(Xfile_declsign_pointer_var)
			if loop.hotgroup=0
				call write(outfile,#and.a,1) #in o, #rezerve/""/{} stays at pointing on first val,modify this behaviour or keep this trick
			end
		end
		call get_string(pbuffer,#sz,#pointer)
		call write(outfile,pointer,sz)
	else #Xfile_declsign_e*
		charx mode#1;call get_char(pbuffer,#mode)
		if mode=(Xfile_declmode_value)
			if field!=(Xfile_declfield_const)
				call writee(outfile)
			else
				call writes(outfile)
			end
			call number(pbuffer,outfile)
		elseif mode=(Xfile_declmode_group)
			set loop.hotgroup 2
			call writeslsr(outfile)
			call writee(outfile)
			call writecl(outfile)
			datax args#1;call get_data(pbuffer,#args)
			call number(pbuffer,outfile)
			sd n=1;while n!=args
				call writec(outfile)
				call number(pbuffer,outfile)
				inc n
			end
			call writecr(outfile)
		else #Xfile_declmode_string
			if type=(Xfile_decltype_byte)
				set loop.hotgroup 2
				call writeslsr(outfile)
			end
			call writee(outfile)
			charx multi#1;call get_multistring(pbuffer,#multi,#sz,#pointer)
			charx trim#1;call get_char(pbuffer,#trim)
			if trim=(Xfile_declmode_string_pop_yes)
				sd b;set b write_allow();if b=(TRUE)
					call write_popstring(pointer,sz,outfile)
				end
				if type!=(Xfile_decltype_byte) #why check here and not above? to let popstring to be normal and check for 0 size and write to file (is more easy at debug)
					call unimplemented("string with no term") # char*={'a','b','c',0} is not "abc" ,downgrade to char? the sizeof and pointers then
				end
			else
				call write_string(outfile,multi,sz,pointer)
			end
		end
	endelse
	if field!=(Xfile_declfield_const)
		call writet(outfile)
	elseif constif=(Xfile_declfield_const_cond)
		call writenl(outfile)
		char a="#endif"^
		call write(outfile,#a,\.a)
	end
	callg writenl(outfile)
end
function action(sd outfile,sv pbuffer)
	charx act#1;call get_char(pbuffer,#act)
	call action1(act,pbuffer,outfile)
	callg writetn(outfile)
end
function action2(sd outfile,sv pbuffer)
	sd replay;set replay pbuffer#
	call varfunc(pbuffer,outfile)

	call writee(outfile)

	charx a2#1;call get_char(pbuffer,#a2)
	if a2!=(Xfile_action2_set)
		if a2=(Xfile_action2_divu)
			set a2 (Xfile_action2_div)
			call write_longcast_uns(outfile,#replay)
		elseif a2=(Xfile_action2_remu)
			set a2 (Xfile_action2_rem)
			call write_longcast_uns(outfile,#replay)
		else
			if a2=(Xfile_action2_mults)
				set a2 (Xfile_action2_mult)
			end
			call write_longcast(outfile,#replay)
		end
		call write(outfile,#a2,1)
	end

	call write_longcast_simple(outfile) #(long)p-log    invalid operands to binary - (have long int and char *)
	call arg(pbuffer,outfile)
	callg writetn(outfile)
end
function call(sd outfile,sv pbuffer)
	char c#1;call get_char(pbuffer,#c)
	if c=(Xfile_call_ret)
		call return(outfile)
	end
	call cll(pbuffer,outfile)
	callg writetn(outfile)
end
function callex(sd *outfile,sv pbuffer)
	call unimplemented("callex")
	char c#1;call get_char(pbuffer,#c)
	call cal(pbuffer) #,outfile
	call varfunc(pbuffer) #,outfile if was not unimplemented
	callg arg(pbuffer) #,outfile
end
function if(sd outfile,sv pbuffer)
	char a="if"^
	call write(outfile,#a,\.a)
	callg cond(pbuffer,outfile)
end
function elseif(sd outfile,sv pbuffer)
	call else_core(outfile)
	#set loop.same_line (TRUE)
	call writes(outfile)
	callg if(outfile,pbuffer)
end
function else(sd outfile)
	call else_core(outfile)
	callg writecln(outfile)
end
function while(sd outfile,sv pbuffer)
	char a="while"^
	call write(outfile,#a,\.a)
	callg cond(pbuffer,outfile)
end
function whiletrue(sd outfile)
	call write(outfile,#while.a,\while.a)
	call writel(outfile)
	char a=asciione
	call write(outfile,#a,1)
	call writer(outfile)
	callg writecln(outfile)
end
function break(sd outfile)
	char a="break"^
	call write(outfile,#a,\.a)
	callg writetn(outfile)
end
function continue(sd outfile)
	char a="continue"^
	call write(outfile,#a,\.a)
	callg writetn(outfile)
end
function end(sd outfile,sv pbuffer)
	charx type#1;call get_char(pbuffer,#type)
	if type=(Xfile_end_func)
		set loop.function_start_base (err)
		set loop.innerfunction (FALSE)
	end
	callg writecrtn(outfile)
end
function ret(sd outfile)
	call write(outfile,#return.a,\return.a-1)
	callg writetn(outfile)
end
function library(sd *outfile,sv pbuffer)
	charx multi#1;datax sz#1;valuex lib#1
	call get_multistring(pbuffer,#multi,#sz,#lib)
end
function import(sd outfile,sv pbuffer)
	charx imp#1;call get_char(pbuffer,#imp)
	charx multi#1;datax sz#1;valuex name#1
	call get_multistring(pbuffer,#multi,#sz,#name)
	call proto_open(outfile,pbuffer)
	charx va#1;call get_char(pbuffer,#va)
	datax arguments#1;call get_data(pbuffer,#arguments)
	if arguments!=0
		call write_long(outfile)
		while arguments!=1
			call writec(outfile)
			dec arguments
			call write_long(outfile)
		end
		if va=(Xfile_function_va_yes)
			call writec(outfile)
		end
	end
	if va=(Xfile_function_va_yes)
		call varargs(outfile)
	end
	callg proto_close(outfile)
end
function import_name(sd *outfile,sv pbuffer)
	datax sz#1;valuex name#1
	call get_string(pbuffer,#sz,#name)
end
function aftercall(sd *outfile,sv pbuffer)
	call unimplemented("aftercall")
	charx acall#1;call get_char(pbuffer,#acall)
	if acall<=(Xfile_aftercall_declarative)
		if acall=(Xfile_aftercall_declare)
			charx feature#1;call get_char(pbuffer,#feature)
			datax sz#1;valuex vr#1
			call get_string(pbuffer,#sz,#vr)
		end
	end
end
function hex(sd *outfile,sv pbuffer)
	call unimplemented("hex")
	datax args#1;call get_data(pbuffer,#args)
	sd n=0;while n!=args
		call number(pbuffer) #,outfile
		inc n
	end
end
function override(sd outfile,sv pbuffer)
	call backindent(outfile)
	datax sz#1;valuex name#1
	call get_string(pbuffer,#sz,#name)
	or loop.allowwrite (allowwrite_no)
	call number(pbuffer,outfile)
	and loop.allowwrite (~allowwrite_no)
end
function orphan(sd *outfile,sv pbuffer)
	charx orph#1;call get_char(pbuffer,#orph)
end
function interrupt(sd outfile)
	#this is not arm #char a="__asm__(\"int $3\");"
	#this is no x86 #__asm__("BKPT");
	char a="__builtin_trap();"^
	#"__builtin_trap();" #this is not continuing on x86 but is an temporary solution
	callg writen(outfile,#a,\.a)
end
function variadic(sv pbuffer,sd *outfile)
	charx type#1;call get_char(pbuffer,#type)
	if type!=(Xfile_variadic_end)
		datax sz#1;valuex name#1
		call get_string(pbuffer,#sz,#name)
	end
end
function proto(sd outfile,sv pbuffer)
	charx type#1;call get_char(pbuffer,#type)
	ss buf;set buf pbuffer#
	or loop.allowwrite (allowwrite_no)
	call proto_open(outfile,#buf)
	call proto_core(outfile,#buf)
	and loop.allowwrite (~allowwrite_no)
	charx ftype#1;call get_char(#buf,#ftype)
	if type=(Xfile_function_t_normal)
		if ftype=(Xfile_proto_void)
			datax sz#1;valuex name#1
			call get_string(pbuffer,#sz,#name)
			call write_void(outfile)
			call write(outfile,name,sz)
			call writel(outfile)
		else
			call proto_open(outfile,pbuffer)
		end
		call proto_core(outfile,pbuffer)
		inc pbuffer#
		callg proto_close(outfile)
	else #Xfile_function_t_struct
		set pbuffer# buf
	end
end

function proto_core(sd outfile,sv pbuffer)
	datax nr#1;call get_data(pbuffer,#nr)
	if nr>^0
		charx field#1;call get_char(pbuffer,#field)
		call decl_runtime_core(pbuffer,outfile,field,(NULL))
		#
		while nr>^1
			call writec(outfile)
			#DA
			call get_char(pbuffer,#field)
			call decl_runtime_core(pbuffer,outfile,field,(NULL))
			dec nr
		end
	end
end
function proto_open(sd outfile,sv pbuffer)
	datax sz#1;valuex name#1
	call get_string(pbuffer,#sz,#name)
	call write_long_pointer(outfile,name,sz)
	callg writel(outfile)
end
function proto_close(sd outfile)
	call writer(outfile)
	callg writetn(outfile)
end

function else_core(sd outfile)
	char a="else"^
	call writecr(outfile)
	callg write(outfile,#a,\.a)
end

function write_allow()
	if loop.ignored=(ignored_no)
		if loop.allowwrite=(allowwrite_yes)
			return (TRUE)
		end
	end
	return (FALSE)
end
function write(sd outfile,ss buf,sd sz)
	sd b;set b write_allow();if b=(TRUE)
		sd items
		if loop.directwrite=(TRUE)
			set items fwrite(buf,sz,1,outfile)
		else
			set items mwrite(buf,sz,outfile)
		end
		if items!=1
			#if sz=0
			call puts("0 size write. or write error") #example declare with unref, but not only
			#end
			call aftercallenable()
		end
	end
end

function writec(sd outfile)
	char c=asciicomma
	callg write(outfile,#c,1)
end
function writecl(sd outfile)
	char c=asciicurlyleft
	callg write(outfile,#c,1)
end
function writecr(sd outfile)
	char a=asciicurlyright
	callg write(outfile,#a,1)
end
function writee(sd outfile)
	callg write(outfile,#cond.eq,1)
end
function writel(sd outfile)
	char a=asciiparenthesisleft
	callg write(outfile,#a,1)
end
function writenl(sd outfile)
	char n=asciinewline
	callg write(outfile,#n,1)
end
function writeq(sd outfile)
	char q=asciidquote
	callg write(outfile,#q,1)
end
function writer(sd outfile)
	char a=asciiparenthesisright
	callg write(outfile,#a,1)
end
function writes(sd outfile)
	char s=asciispace
	callg write(outfile,#s,1)
end
function writesl(sd outfile)
	char left=asciisquareleft
	callg write(outfile,#left,1)
end
function writesr(sd outfile)
	char right=asciisquareright
	callg write(outfile,#right,1)
end
function writet(sd outfile)
	char sc=asciisemicolon
	callg write(outfile,#sc,1)
end

function unsigned(sd outfile)
	char a="unsigned "^
	callg write(outfile,#a,\.a)
end
function unsigned_type(sd outfile,sd type,sd sign)
	if sign=(Xfile_declext_no)
		#the problem with char is that it is signed char on arm and unsigned char on x86
		#if type<=(Xfile_decltype_longByte); ret; end

		callg write(outfile,#unsigned.a,\unsigned.a)
		ret
	end
	if type<=(Xfile_decltype_longByte)
		ss s=un_chars_size
		add s #unsigned.a
		callg write(outfile,s,\unsigned.a-un_chars_size)
	end
end
function write_longcast_simple(sd outfile)
	call writel(outfile)
	call write_long(outfile)
	callg writer(outfile)
end
function write_longcast_ex_ex(sd outfile,sd pbuffer,sv sign,sv fn)
	call writel(outfile)
	call sign(outfile)
	call write_long(outfile)
	call writer(outfile)
	callg fn(pbuffer,outfile)
end
function write_longcast_ex(sd outfile,sd pbuffer,sv sign)
	callg write_longcast_ex_ex(outfile,pbuffer,sign,varfunc)
end
function write_longcast_uns(sd outfile,sd pbuffer)
	callg write_longcast_ex_ex(outfile,pbuffer,unsigned,varfunc)
end
function write_longcast_arg(sd outfile,sd pbuffer)
	callg write_longcast_ex_ex(outfile,pbuffer,unsigned,arg)
end
function write_longcast(sd outfile,sd pbuffer)
	callg write_longcast_ex(outfile,pbuffer,signed)
end
function write_void(sd outfile)
	char a="void "^
	callg write(outfile,#a,\.a)
end

function writecln(sd outfile)
	inc loop.indentation
	call writecl(outfile)
	callg writenl(outfile)
end
function writen(sd outfile,ss buf,sd sz)
	call write(outfile,buf,sz)
	callg writenl(outfile)
end
function writeslsr(sd outfile)
	call writesl(outfile)
	callg writesr(outfile)
end
function writetn(sd outfile) #terminator newline
	call writet(outfile)
	callg writenl(outfile)
end
function writecrtn(sd outfile)
	call writecr(outfile)
	callg writetn(outfile) #with termination only for struct
end

function fprintu(sd outfile,sd c)  #has an upper write_allow check
	sd items;set items fprintf(outfile,"%u",c) #safe from directwrite at this moment
	if items<0
		call aftercallenable()
	end
end
